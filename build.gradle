buildscript {
    repositories {
        mavenCentral()
    }
    configurations.classpath {
        resolutionStrategy.force(
                'org.ow2.asm:asm:9.6',
                'org.ow2.asm:asm-commons:9.6',
                'org.ow2.asm:asm-tree:9.6',
                'org.ow2.asm:asm-analysis:9.6',
                'org.ow2.asm:asm-util:9.6'
        )
    }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.zxcmc.exort'
version = '0.8.2'
description = 'Exort Storage Network'

base {
    archivesName = 'Exort'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven {
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        url = 'https://maven.enginehub.org/repo/'
    }
}

configurations {
    googleJavaFormat
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT'
    compileOnly 'org.xerial:sqlite-jdbc:3.45.3.0'
    implementation 'org.bstats:bstats-bukkit:3.1.0'
    implementation 'org.bstats:bstats-base:3.1.0'
    googleJavaFormat 'com.google.googlejavaformat:google-java-format:1.22.0'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs += [
        "-Xlint:deprecation",
        "-Xlint:unchecked"
    ]
}

tasks.register('googleJavaFormat', JavaExec) {
    group = 'formatting'
    description = 'Formats Java sources using google-java-format.'
    classpath = configurations.googleJavaFormat
    mainClass = 'com.google.googlejavaformat.java.Main'
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(21)
    }
    jvmArgs(
        '--add-exports', 'jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED',
        '--add-exports', 'jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED',
        '--add-exports', 'jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED',
        '--add-exports', 'jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED',
        '--add-exports', 'jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED',
        '--add-exports', 'jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED'
    )
    args '--replace'
    args fileTree('src/main/java') {
        include '**/*.java'
    }.files
}

tasks.withType(JavaCompile).configureEach {
    dependsOn tasks.named('googleJavaFormat')
}

tasks.named('shadowJar') {
    archiveClassifier = ''
    configurations = [project.configurations.runtimeClasspath]
    dependencies {
        include(dependency('org.bstats:bstats-bukkit:3.1.0'))
        include(dependency('org.bstats:bstats-base:3.1.0'))
    }
    relocate 'org.bstats', 'com.zxcmc.exort.libs.bstats'
}

tasks.named('jar') {
    enabled = false
}

tasks.named('build') {
    dependsOn tasks.named('shadowJar')
}

tasks.processResources {
    filteringCharset = 'UTF-8'
    inputs.property('version', project.version)
    inputs.property('description', project.description)
    filesMatching(['plugin.yml', 'paper-plugin.yml']) {
        expand(
            version: project.version,
            description: project.description
        )
    }
}
